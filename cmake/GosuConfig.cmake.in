# Find Gosu includes and library
#
# This module defines
#  GOSU_INCLUDE_DIRS
#  GOSU_DEPENDENCIES, the libraries to link against to use Gosu.
#  GOSU_FOUND, If false, do not try to use Gosu
#

include(FindPackageMessage)

# TODO - why is this necessary?
get_filename_component(THIS_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
if(WIN32)
    set(PREFIX ${THIS_DIR}/..)
else()
    set(PREFIX ${THIS_DIR}/../../..)
endif()

set(GOSU_INCLUDE_DIR "${PREFIX}/@GOSU_INCLUDE_INSTALL_DIRS@/..")
set(GOSU_INCLUDE_DIRS ${GOSU_INCLUDE_DIR})

find_package(OpenAL REQUIRED)
find_package(OpenGL REQUIRED)

find_package(PkgConfig REQUIRED)
pkg_check_modules(SDL2 REQUIRED sdl2)
pkg_check_modules(VORBIS REQUIRED vorbisfile)

set(GOSU_DEPENDENCIES
    ${SDL2_LIBRARIES}
    ${OPENGL_LIBRARY}
    ${OPENAL_LIBRARY}
    ${VORBIS_LIBRARIES}
)

if(NOT WIN32 AND NOT APPLE)
    find_package(Freetype REQUIRED)
    pkg_check_modules(SDL2_TTF REQUIRED SDL2_ttf)
    find_package(X11 REQUIRED)
    pkg_check_modules(PANGO REQUIRED pango)
    pkg_check_modules(PANGOFT REQUIRED pangoft2)
    pkg_check_modules(SNDFILE REQUIRED sndfile)
    
    # FreeImage has neither either a Find*.cmake file nor a .pc file - search manually
    # pkg_check_modules(FREEIMAGE REQUIRED freeimage)
    find_path(FREEIMAGE_INCLUDE_DIRS FreeImage.h)
    find_library(FREEIMAGE_LIBRARIES freeimage)
    if(NOT FREEIMAGE_INCLUDE_DIRS OR NOT FREEIMAGE_LIBRARIES)
        message(FATAL_ERROR "FreeImage could not be found")
    endif()
    mark_as_advanced(FREEIMAGE_INCLUDE_DIRS)
    mark_as_advanced(FREEIMAGE_LIBRARIES)
    
    set(GOSU_DEPENDENCIES ${GOSU_DEPENDENCIES}
        ${FREETYPE_LIBRARIES}
        ${SDL2_TTF_LIBRARIES}
        ${SNDFILE_LIBRARIES}
        ${PANGO_LIBRARIES}
        ${PANGOFT_LIBRARIES}
        ${FREEIMAGE_LIBRARIES}
    )
endif ()

if(APPLE)
    find_library(FOUNDATION_LIBRARY Foundation)
    find_library(COCOA_LIBRARY Cocoa)
    find_library(CARBON_LIBRARY Carbon)
    find_library(APPLICATION_SERVICES_LIBRARY ApplicationServices)
    find_library(AUDIO_TOOLBOX_LIBRARY AudioToolbox)
    find_library(ICONV_LIBRARY iconv)
    mark_as_advanced(
        FOUNDATION_LIBRARY
        COCOA_LIBRARY
        CARBON_LIBRARY
        APPLICATION_SERVICES_LIBRARY
        AUDIO_TOOLBOX_LIBRARY
        ICONV_LIBRARY
    )
    set(GOSU_DEPENDENCIES ${GOSU_DEPENDENCIES}
        ${FOUNDATION_LIBRARY}
        ${COCOA_LIBRARY}
        ${CARBON_LIBRARY}
        ${APPLICATION_SERVICES_LIBRARY}
        ${AUDIO_TOOLBOX_LIBRARY}
        ${ICONV_LIBRARY}
    )
endif()

set(GOSU_LIBRARY "gosu")
set(GOSU_LIBRARIES "${GOSU_LIBRARY}")
find_package_handle_standard_args(Gosu DEFAULT_MSG GOSU_LIBRARY GOSU_INCLUDE_DIR)
mark_as_advanced(GOSU_INCLUDE_DIR GOSU_LIBRARY)
